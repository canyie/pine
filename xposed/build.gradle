apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdk rootProject.ext.compileSdk
    namespace "top.canyie.pine.xposed"
    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    sourceSets {
        main {
            java.srcDirs += ['src/main/apacheCommonsLang']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/**'
            excludes += '/*.properties'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly project(':core')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group rootProject.groupId
            artifactId 'xposed'
            version '0.0.9'
            // the components are created only during the afterEvaluate phase
            afterEvaluate {
                from components.release
            }
            pom {
                name = rootProject.repoName
                description = 'pine hook xposed compat'
                url = rootProject.website
                licenses rootProject.licenses
                developers rootProject.developers
                scm rootProject.scm
            }
        }
    }
    repositories rootProject.uploadRepositories
}

signing {
    sign publishing.publications.mavenJava
}
